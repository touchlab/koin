/*
 * Copyright 2018 Russell Wolf
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import org.gradle.api.publish.maven.internal.artifact.FileBasedMavenArtifact
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

def pomConfig = {
  licenses {
    license {
      name "The Apache Software License, Version 2.0"
      url "http://www.apache.org/licenses/LICENSE-2.0.txt"
      distribution "repo"
    }
  }
}

if (hasProperty('bintray_user') && hasProperty('bintray_apikey')) {
  bintray {
    user = bintray_user
    key = bintray_apikey

    override = true
    publish = true

    pkg {
      userOrg = bintray_org
      repo = bintray_repo
      name = archivesBaseName
      licenses = ['Apache-2.0']
      vcsUrl = bintray_github
    }
  }

  afterEvaluate {
    project.publishing.publications.forEach { publication ->
      publication.pom.withXml {
        def root = asNode()
        root.appendNode('name', archivesBaseName)
        root.appendNode('description', 'Koin')
        root.children().last() + pomConfig
      }
    }
  }

  bintrayUpload.doFirst {
    publishing.publications.withType(MavenPublication).forEach({ publication ->
      File moduleFile = new File(project.buildDir, "publications/${publication.name}/module.json")
      if (moduleFile.exists()) {
        publication.artifact(new FileBasedMavenArtifact(moduleFile) {
          protected String getDefaultExtension() {
            return 'module';
          }
        })
      }
    })
    publications = project.publishing.publications
  }
}